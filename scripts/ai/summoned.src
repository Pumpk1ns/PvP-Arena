use uo;
use os;


include "include/eventID";
include "include/attributes";
include "include/sysEvent";
include "include/NPCBackpacks";
include "include/randName";
Const me := Self();
include "include/random";
include "include/distance";
include "include/frakcja";
include "include/spellsetup";
include "include/eventy";
include "include/yesNo";

const HALT_THRESHOLD := 10;
const MOVING_EFFECT_FIREBALL  := 0x36d4;

var masterserial := GetObjProperty( me, "master" );
if (!masterserial)
	GoWild();
endif


var master := SystemFindObjectBySerial( masterserial );
if( !master )
	master := 0;
	GoWild();
endif

var mypack := me.backpack;

var caster := 0;
var saywords;
var firebreather;
var poisoner;
var summons := 0;
var chaseloop := 0;

var guarding :=0;
var following := 0;
var staying := 0;
var nextwatch := ReadGameClock();
var freetime := CInt(ReadGameClock() + 2400);

me.setmaster( master );

if( me.name["<random>"] )
	SetName( me, RandomName( me ) );
endif
if (!me.backpack)
	var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
	EquipItem(me, newbackpack);
endif

if (GetObjProperty(me,"freetime"))
	freetime := GetObjProperty(me,"freetime");
else
	freetime := CInt(ReadGameClock() + 2400);
	SetObjProperty(me,"freetime",freetime);
endif

var dragonspeak := 0;
if ( me.npctemplate["dragon"] )
	dragonspeak := 1;
endif

program TamedAI()

	if (master.isA(POLCLASS_NPC))
		if (GetObjProperty(me,"#restarting"))
			GoWild();
		else
			SetObjProperty( me,"master", GetObjProperty(master,"master") );
			SetObjProperty(me,"#restarting",1);
			RestartScript(me);
		endif
	endif
	EraseObjProperty(me,"#restarting");

	if (GetObjProperty(me, "frozen"))
		me.frozen := 1;
	endif

	SetObjProperty(me,"color",me.color);

	if ( GetObjProperty( me, "regspawn" ) )
		var spawnname := GetObjProperty( me, "regspawn" );
		var numspawns := CInt(GetGlobalProperty(spawnname))-1;
		SetGlobalProperty(spawnname,numspawns);
		EraseObjProperty( me, "regspawn" );
	endif

	SetAnchor( me.x, me.y, 20, 0 );

	if (npccfgfile[me.npctemplate].script == "spellKillPCs")
		caster := 1;
		spells := GetConfigStringArray( npccfgfile[me.npctemplate], "spell" );
		saywords := npccfgfile[me.npctemplate].saywords;
	elseif (npccfgfile[me.npctemplate].script == "firebreather")
		firebreather := 1;
//	elseif (npccfg[me.npctemplate].script == "poisonkillpcs")
//		poisoner := 1;
	endif

	if (!GetObjProperty(me,"#nextpoop"))
		SetObjProperty(me,"#nextpoop",ReadGameClock() + CInt(RandomInt(1200)));
	endif

	EnableEvents( SYSEVENT_SPEECH, 12 );
	EnableEvents( SYSEVENT_ENGAGED + SYSEVENT_DAMAGED );
	EnableEvents( SYSEVENT_DOUBLECLICKED );
  EnableEvents( EVID_PEACEMADE );
  EnableEvents( EVID_ALL_GUARD_CMD );  

	SetWarMode( 0 );
	SetObjProperty(me, "serial", me.serial);

	MainAILoop();

endprogram

function Fight( opponent )

	var oldfollowing := following;

	SetDexterityMod(me, 0);
	following := 0;

	if ( (opponent.serial == me.serial) || (opponent.serial == master.serial) )
		return;
	endif

	TurnToward( opponent );
	SetOpponent( opponent );

	var nextbreath := ReadGameClock();
	var ev;

	var loops := 0;
	var waittime := 120;

	while ( (opponent) &&
	        not (opponent.dead || opponent.hidden || opponent.concealed) &&
	        (Distance(me,opponent) < 20) )


		if (!CloseDistance( opponent ) )
			loops := loops + 1;
			waittime := 0;
		else
			loops := 0;
			waittime := 1;
		endif

		ev := wait_for_event( waittime );

		case (ev.type)
			EVID_ALL_ATTACK_CMD:
				opponent := ev.target;
				SetOpponent( ev.target );
			EVID_ALL_FOLLOW_CMD:
				following := ev.target;
			EVID_ALL_GUARD_CMD:
				guarding := ev.target;
				PrintTextAbove(me,"*ochrania " + guarding.name + "*");
			SYSEVENT_SPEECH:
				ProcessSpeech(ev);
			SYSEVENT_ENGAGED:
				if (ev.source)
					if ( (ev.source.serial != opponent.serial) && (ev.source.serial != master.serial) &&
					     (ev.source.serial != me.serial) )
						if( (!CheckLineOfSight( me, opponent )) || (RandomInt(8)==1) )
							opponent := ev.source;
							SetOpponent( opponent );
						endif
					endif
					TurnToward( opponent );
				endif
			SYSEVENT_DAMAGED:
				if(!me.criminal && !me.master.criminal)
					ev.source.SetCriminal(1);
				endif
				if (ev.source)
					if ( (ev.source.serial != opponent.serial) && (ev.source.serial != master.serial) &&
					     (ev.source.serial != me.serial) )
						if( (!CheckLineOfSight( me, opponent )) || (RandomInt(8)==1) )
							opponent := ev.source;
							SetOpponent( opponent );
						endif
					endif
					TurnToward( opponent );
				endif
			EVID_PEACEMADE:
				SetWarMode( 0 );
				opponent := 0;
				following := oldfollowing;
				return;
		endcase

		if (following)
			SetWarMode( 0 );
			opponent := 0;
			staying := 0;
			return;
		endif

		if (firebreather)
			if ( nextbreath <= ReadGameClock() )
        Ogniuj(me, opponent);
				nextbreath := ReadGameClock()+RandomInt(5)+5;
			endif
		elseif (caster)
			if ( nextbreath <= ReadGameClock() )
				sleep(1);
				Czaruj(me, opponent);
				nextbreath := ReadGameClock()+RandomInt(10)+10;
			endif
		elseif (poisoner)
			if ( nextbreath <= ReadGameClock() )
				nextbreath := ReadGameClock()+RandomInt(10)+10;
			endif
		endif

		if ( loops > 100 )
			flee(opponent);
			return;
		endif

	endwhile

	SetWarMode( 0 );
	opponent := 0;

	foreach mob in ListHostiles( me, 9, 0 )
		Fight(mob);
		return;
	endforeach
	
	foreach mob in ListMobilesNearLocation( me.x, me.y, me.z, 8, _DEFAULT_REALM );
		if(mob.npctemplate && mob.script != "playerMerchant" && mob.alignment == "evil")
			Fight(mob);
		endif
	endforeach

	if ( Distance(me, master) < 8 )
		following := oldfollowing;
	else
		following := 0;
		staying := 0;
	endif

	

endfunction

function CloseDistance( opponent )
	var sleepdelay := 300 - (CInt(GetDexterity(me)) * 1.5);
	if (sleepdelay < 0) sleepdelay := 0; endif

	if( Distance( me, opponent ) <= 1)
		return 1;
	else
		if(!RunToward( opponent ))
			sleepms(sleepdelay);
			return 1;
		else
			return 0;
		endif
	endif

endfunction


function Transfer()

	if( GetObjProperty( me, "summoned" ) ||
	    GetObjProperty( me, "animated" ) ||
	    GetObjProperty( me, "charmed" ) )
		return;
	endif

	SendSysMessage( master, "Komu przekazac " + me.name + " ?",3,63 );
	var whom := Target( master, TGTOPT_NOCHECK_LOS );
	if(!whom)
		SendSysMessage( master, "Anulowano",3,40 );
		return;
	endif

	if (!YesNo(whom,"Przyjmujesz " + master.name + " ?"))
		SendSysMessage( master, whom.name + " nie przyjal zwierzaka.",3,40 );
		return;
	endif

	if ( (firebreather) || (caster) )
  
    var stat, nazwa;
    var magery := GetEffectiveSkill(whom, SKILLID_MAGERY);
    var sword := GetEffectiveSkill(whom, SKILLID_SWORDSMANSHIP);

    if(magery > 30)
      stat := Cint(GetBaseIntelligence(whom));
      nazwa := "inteligencji";
    elseif(sword > 30)
      stat := Cint(GetBaseDexterity(whom));
      nazwa := "zrecznosci";
    endif

	var tameskill := npccfgfile[me.npctemplate].tameskill;
	var tgttaming := GetEffectiveSkill(whom, SKILLID_TAMING)+20;
	if ( Cint(stat/1.6) < tameskill )
    SendSysMessage(whom, "Cel ma za malo " + nazwa + " aby przyjac to stworzenie. Jego wartosc oswajania wynosi " + Cint(stat/1.6) + ". Potrzeba " + (tameskill) +".",3,40);
		return;
	endif

	endif

	SetObjProperty( me, "master", whom.serial );
	PrintTextAbove(me, CStr(me.name + " akceptuje " + whom.name+ " jako swojego mistrza"),3,63);
	RestartScript( me );

endfunction


function Release()

	var summoned := GetObjProperty( me, "summoned" );
	var animated := GetObjProperty( me, "animated" );
	var totem := CInt(GetObjProperty( me, "totem" ));
	if( totem )
		set_critical(1);
		var it := CreateItemAtLocation(me.x,me.y,me.z,totem,1);
		if (it)
			it.decayAt := 0;
			it.name := me.name;
			it.color := CInt(GetObjProperty( me, "totemcolor" ));
			SetObjProperty(it,"critter",me.npctemplate);
			SetObjProperty(it,"critcolor",me.color);
			SetObjProperty(it,"totemhp",GetHp(me));
			it.decay := 0;
			it.movable := 1;
			PlaySoundEffect( me, SFX_SPELL_DISPEL );
			PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xa, 0xa );
			MoveObjectToLocation(me,5288, 1176, 0,_DEFAULT_REALM, 4);
			RevokePrivilege( me, "invul" );
			ApplyRawDamage( me, GetMaxHp(me) + 3 );
		set_critical(0);
		else
			say("*Argh*");
			return;
		endif
	elseif( summoned )
		PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xa, 0xa );
		MoveObjectToLocation(me,5288, 1176, 0,_DEFAULT_REALM, 4);
		RevokePrivilege( me, "invul" );
		ApplyRawDamage( me, GetHp(me) + 3 );
	elseif( animated )
		RevokePrivilege( me, "invul" );
		ApplyRawDamage( me, GetHp(me) + 3 );
	else
		GoWild();
	endif

endfunction

function Fetch()

	if ( len(EnumerateItemsInContainer(mypack)) )
		drop();
	endif

	var tobj := Target(master);
	if (tobj.container)
		return;
	endif

	var times := 0;
	while ( (Distance(me, tobj) > 1) && times < 5 && CheckLineOfSight(me,tobj) )
		if( !RunToward(tobj))
			sleep(1);
			times := times + 1;
		else
			times := 0;
		endif
	endwhile

	if (Accessible(me,tobj))
		MoveItemToContainer(tobj, mypack);
	endif

endfunction


function drop()

	foreach myitems in EnumerateItemsInContainer(mypack)
		if (myitems.container.serial == mypack.serial)
			MoveObjectToLocation(myitems, me.x, me.y, me.z,_DEFAULT_REALM, 1);
			sleepms(100);
		endif
	endforeach

endfunction

function speak()

	PetCount();

	if (RandomInt(4)==1)
		var sound_number := npccfgfile[me.npctemplate].attackhitsound;
		if (sound_number)
			PlaySoundEffect( me, sound_number );
		endif
	endif

endfunction

function PetCount()
  var pets := 0;
  var mobs := ListMobilesNearLocation(me.x,me.y,me.z,8);
  foreach mob in mobs
    if((mob.script == "tamed")&&(GetObjProperty(mob,"master")==masterserial))
      if(GetObjProperty(mob, "summoned"))
        pets := pets + 0;
      else
        pets := pets+5;
      endif
    endif
  endforeach
  return pets;
endfunction


function OpenMyPack();

	if (!master)
		master := SystemFindObjectBySerial( masterserial );
		me.setmaster( master );
	endif

	if (Distance(me,master) > 1)
		return;
	endif

	case (me.graphic)
		0x123:
		0x124:
			if (!me.backpack)
				var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
				EquipItem(me, newbackpack);
			endif

			foreach item in EnumerateItemsInContainer(mypack)
				if (item.container.serial == mypack.serial)
					MoveItemToContainer(item, me.backpack);
				endif
			endforeach

			SendOpenSpecialContainer( master, me.backpack );
			return;
	endcase

  if(GetObjProperty(master, "poly") == 2)
    SendSysMessage(master, "Jak w tej formie wsiadziesz na wierzchowca?",3,40);
    return 0;
  endif

  if(GetObjProperty(master, "#reaper"))
    SendSysMessage(master, "Jak w tej formie wsiadziesz na wiechowca?");
    return 0;
  endif
     
	master.hidden := 0;

	var alreadymounted := GetEquipmentByLayer( master, 25 );
	if (alreadymounted)
		return;
	endif

	var mounttype := 0;

	case (me.graphic)
      0xcc: mounttype := 0x3ea2;  // Horse
      0xc8: mounttype := 0x3e9f;  // Horse
      0xe2: mounttype := 0x3ea0;  // Horse
      0xe4: mounttype := 0x3ea1;  // Horse
      0xdc: mounttype := 0x3ea6;  // Llama
      0xd2: mounttype := 0x3ea3;  // Desert Ostard
      0xda: mounttype := 0x3ea4;  // Fire etc Ostard
      0xdb: mounttype := 0x3ea5;  // Ice etc Ostard
      177:  mounttype := 0x3ea7;  // Nightmare
      117:  mounttype := 0x3ea8;  // Silver Steed
      114:  mounttype := 0x3ea9;  // Nightmare
      115:  mounttype := 0x3eaa;  // Ethereal Horse
      170:  mounttype := 0x3eab;  // Ethereal Llama
      171:  mounttype := 0x3eac;  // Ethereal Ostard
     0xC3:  mounttype := 0x3e97;  // Ethereal Giant Beetle
     0xC2:  mounttype := 0x3e98;  // Ethereal Swamp Dragon
     0xC1:  mounttype := 0x3e9a;  // Ethereal Ridgeback
     0xBF:  mounttype := 0x3e9c;  // Ethereal Kirin
     0xC0:  mounttype := 0x3e9d;  // Ethereal Unicorn
     0xC8:  mounttype := 0x3e9f;  // Light Brown horse
      120:  mounttype := 0x3eaf;  // War Horse (Blood Red)
      121:  mounttype := 0x3eb0;  // War Horse (Light Green)
      119:  mounttype := 0x3eb1;  // War Horse (Light Blue)
      144:  mounttype := 0x3eb3;  // Sea Horse (Medium Blue)
      116:  mounttype := 0x3eb5;  // Nightmare
      178:  mounttype := 0x3eb6;  // Nightmare 4
      132:  mounttype := 0x3ead;  // Kirin
      118:  mounttype := 0x3eb2;  // War Horse (Purple)
      122:  mounttype := 0x3eb4;  // Unicorn
      116:  mounttype := 0x3eb5;  // Nightmare
      179:  mounttype := 0x3eb7;  // Dark Steed
      187:  mounttype := 0x3eb8;  // Ridgeback
      188:  mounttype := 0x3eba;  // Ridgeback, Savage
      793:  mounttype := 0x3ebb;  // Skeletal Mount
      791:  mounttype := 0x3ebc;  // Beetle
      794:  mounttype := 0x3ebd;  // SwampDragon
      799:  mounttype := 0x3ebe;  // Armored Swamp Dragon
      0x11C: mounttype := 0x3EBF;
      0x114: mounttype := 0x3EC0;
      0xF8: mounttype := 0x3EC1;
      0xF3: mounttype := 0x3EC2;

	endcase
  
  if(master.hidden)
    master.hidden := 0;
  endif
    
  var widmowiec := Cint(GetObjProperty(me, "widmak"));
  
    if(me.graphic == 115 || me.graphic == 171 || me.color == 0x4001 || widmowiec)
      SetObjProperty(master, "widmak", 1);
    else
      EraseObjProperty(master, "widmak");
    endif 
    
	if (mounttype)
		if ( MoveObjectToLocation(master,me.x,me.y,me.z,_DEFAULT_REALM) )

			set_critical(1);
			master.facing := me.facing;
			var mount := CreateItemAtLocation(5288, 1176, 0, 0xf021,1);
			mount.color := me.color;
        SetObjProperty(mount, "template", me.npctemplate);
        SetObjProperty(mount, "nazwa", me.name);
        SetObjProperty(mount, "Hp", GetHp(me));
        SetObjProperty(mount, "Mana", GetMana(me));
        SetObjProperty(mount, "Stamina", GetStamina(me));
        SetObjProperty(mount, "color", me.color);
        SetObjProperty(mount, "truecolor", me.truecolor);
        SetObjProperty(mount, "freetime", freetime);
			MoveObjectToLocation(me, 5288, 1176, 0, _DEFAULT_REALM, 4);
			mount.graphic := mounttype;
			EquipItem(master,mount);
			RevokePrivilege( me, "invul" );
			SetObjProperty(me,"guardkill",1);
			ApplyRawDamage( me, GetMaxHp(me) + 3 );

			set_critical(0);
			sleep(1);

			MoveObjectToLocation(me, 5288, 1176, 0, _DEFAULT_REALM, 4);
			RevokePrivilege( me, "invul" );
			SetObjProperty(me,"guardkill",1);
			ApplyRawDamage( me, GetMaxHp(me) + 3 );

		endif
	endif

endfunction

function poop();

	if ( ReadGameClock() > CInt(GetObjProperty(me,"#nextpoop")) )
		if (RandomInt(4)==1)

			var poopoo := 0;
			case (me.graphic)
				0xcc:
				0xe4:
				0xc8:
				0xe2:
				0x123:	poopoo := CreateItemAtLocation(me.x,me.y,me.z,0x0f3c,1);
				default: return;

			endcase

			if (poopoo)
				poopoo.decayAt := ReadGameClock() + 300;
				poopoo.movable := 0;
			endif

		endif
	endif
  PrintTextAbove(me, "*zesral sie przestrasznie*",3,40);
	SetObjProperty(me,"#nextpoop",ReadGameClock() + 1800);

endfunction


function MainAILoop()

	var ev;
	//	var mename := lower(me.name);
	var warnings := 0;

	nextwatch := ReadGameClock();
	var waittime := 120;

	if (!GoToSleep() )
		waittime := 2;
	endif

	var loops := 60;
	var firstwander := 1;

	while (1)
		ev := os::wait_for_event( waittime );
		if ( ev )
			if (!ev.source.npctemplate)
				waittime := 2;
				loops := 0;
			endif
			case (ev.type)
				SYSEVENT_SPEECH:
					ProcessSpeech(ev);
				SYSEVENT_ENGAGED:
				SYSEVENT_DAMAGED:
					if (ev.source)
            Fight( ev.source );
					endif
				SYSEVENT_DOUBLECLICKED:
					if (ev.source.serial == masterserial)
						OpenMyPack();
					endif
				EVID_ALL_ATTACK_CMD:
					Fight(ev.target);
				EVID_ALL_FOLLOW_CMD:
					following := ev.target;
				EVID_ALL_GUARD_CMD:
					guarding := ev.target;
					PrintTextAbove(me,"*ochrania " + guarding.name + "*");
			endcase
		endif

		if ( (following) && (master) )
			waittime := 0;
			firstwander := 1;
			staying := 0;
			loops := 0;
			SetAnchor( me.x, me.y, 20, 0 );
			Follow();
		elseif ( (guarding) && (master) )
			waittime := 7;
			firstwander := 1;
			loops := 0;
			SetAnchor( me.x, me.y, 20, 0 );
			Guard();
		else
			following := 0;
			guarding := 0;
			if ( firstwander )
				SetAnchor( me.x, me.y, 2, 80 );
				waittime := 2;
			endif
			firstwander := 0;
			if ((master) && (!staying))
				wander();
			endif
			EraseObjProperty(me,"#flees");
			loops := loops + 1;
		endif

		if (loops > 60)
			if ( GoToSleep() )
				waittime := 120;
			else
				waittime := 2;
				loops := 0;
			endif
		endif

	// Poop();

	endwhile

endfunction

function Guard()

	if ( nextwatch <= ReadGameClock() )
		nextwatch := ReadGameClock() + 7;
		if ( RandomInt(20)==1 )
			PrintTextAbove(me,"*ochrania " + guarding.name + "*");
		endif
		foreach mob in ListHostiles( guarding, 9, 0 );
			Fight(mob);
			return;
		endforeach
	endif

	if ( (!GetStrength(guarding)) || (guarding.dead) || (Distance(me,guarding) >
15) )
		guarding := 0;
	endif

endfunction

function Follow()

	var strongwill:=0;

        if ( me.npctemplate["dragon1"] || me.npctemplate["dragon2"]||me.npctemplate["nightmare"])//too strong to exploit with
        	strongwill := 1;
        endif

        if(following.warmode && !strongwill)
        	SendSysMessage(following, "The creature is hesitant to follow you");//doesn't want to follow an angry master
        	RestartScript(me);//he is constantly being flooded with follows, so most effective way is to restartscript
        	return;
	endif

	if ( (me.frozen) )
		sleep(10);
		return;
	endif

	SetDexterityMod(me, 5000);
	var d := Distance( me, following );
	if (d < 2)
		sleep(1);
		chaseloop := 0;
	elseif (( d > 10 ) || ( chaseloop > 50 ))
		MoveObjectToLocation( me, following.x, following.y, following.z, _DEFAULT_REALM, 4 );
		chaseloop := 0;
		sleep(1);
	else
		RunToward(following);
		chaseloop := chaseloop + 1;
	endif

	if ( (!GetStrength(following)) || (following.dead) )
		following := 0;
	endif
	SetDexterityMod(me, 0);
	if (guarding)
		Guard();
	endif

endfunction

function ProcessSpeech(ev)

	if( ev.source.serial != masterserial )
		return;
	endif

	if (!master)
		RestartScript( me );
	endif

	speak();

	var NumNr := 0;
	foreach mob in ListMobilesNearLocation(me.x, me.y, me.z, 12)
		if(mob.script == "summoned" && ev.source.serial == masterserial)
			NumNr := NumNr + 1;
		endif
  endforeach

	if(NumNr > 1)
		SendSysMessage(ev.source, "Nie mozesz kontrolowac takiej ilosci stworzen", 3, 40);
		guarding := 0;
		following := 0;
		foreach mob in ListMobilesNearLocation(me.x, me.y, me.z, 12)
			if(mob.script == "summoned" && ev.source.serial == masterserial)
				RestartScript(mob);
			endif
		endforeach
	endif
	
	var evtext := lower(ev.text);
	var mename := lower(me.name);

	if (evtext[mename + " zabij"] || evtext[mename + " atakuj"] ||
	    evtext["zabijcie"] || evtext["atakujcie"] )
		var what := Target( master, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
		if (what)
			
			if (evtext["zabijcie"] || evtext["atakujcie"] )
				AllCommand(EVID_ALL_ATTACK_CMD, what);
				SetObjProperty(me, "guardignore" , 0);
			else
				Fight( what );
				SetObjProperty(me, "guardignore" , 0);
			endif
		endif
	elseif( evtext[mename + " stoj"] || evtext["stojcie"])
		guarding := 0;
		following := 0;
	elseif ((evtext[mename + " podejdz"]) || (evtext["podejdzcie"]))
		var done := 0;
		chaseloop := 0;
		while( done == 0 )
			chaseloop := chaseloop +1;
			done := CloseDistance( ev.source );
			if (chaseloop > 25)
				done :=1;
			endif
		endwhile
	elseif((evtext[mename + " idz"]) || (evtext["idzcie"]))
		SendSysMessage(ev.source, "Gdzie ma isc?", 3, 63);
		var cel := TargetCoordinates( ev.source );
		following := 0;
		guarding := 0;
		while( me.x != cel.x && me.y != cel.y )
			WalkTowardLocation( cel.x, cel.y );
			sleepms(25);
		endwhile
	elseif (evtext[mename + " podazaj"])
		if (evtext["podazaj za mna"])
			following := ev.source;
		else
			var what := Target( master, TGTOPT_CHECK_LOS );
			if (what)
				following := what;
			endif
		endif
  elseif (evtext["podazajcie"])
		if (evtext["podazajcie za mna"])
			following  := ev.source;
		else
			var what := Target( master, TGTOPT_CHECK_LOS );
			if (what)
				AllCommand(EVID_ALL_FOLLOW_CMD, what);
			endif
		endif
		
	elseif (evtext[mename + " zaakceptuj"] || (evtext["zaakceptujcie"]))
		Transfer();
	elseif (evtext[mename + " jestes wolny"] || (evtext["jestescie wolne"]))
		Release();
	elseif (evtext[mename + " ochraniaj"])
		if (evtext["ochraniaj mnie"])
			guarding := ev.source;
			PrintTextAbove(me,"*ochrania " + guarding.name + "*");
		else
			var what := Target( master, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			if (GetHp(what))
				guarding := what;
				PrintTextAbove(me,"*ochrania " + guarding.name + "*");
			endif
		endif
	elseif (evtext["ochraniajcie mnie"])
		if (evtext["ochraniaj mnie"])
			guarding := ev.source;
			PrintTextAbove(me,"*ochrania " + guarding.name + "*");
		else
			var what := Target( master, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			if (GetHp(what))
				AllCommand(EVID_ALL_GUARD_CMD, what);
			endif
		endif
	elseif (evtext[mename + " wez"]  || (evtext[mename + " zaloz"]))
		
		Fetch();
	elseif (evtext[mename + " upusc"])
		
		drop();
	elseif (evtext[mename + " daj glos"]  || (evtext["dajcie glos"]))
		speak();
	elseif (evtext[mename + " czekaj"]  || (evtext["czekajcie"]))
		staying := 1;
	endif

endfunction

function AllCommand(evtype,what)

	var mobs := ListMobilesNearLocation( master.x, master.y, master.z, 9 );
	var eve := {};
	eve.+type := evtype;
	eve.+source := me;
	eve.+target := what;
	SendEvent( me, eve );
	foreach mob in mobs
		if( GetObjProperty( mob, "master" ) == me.master.serial )
			SendEvent( mob, eve );
		endif
	endforeach

endfunction



function flee(opponent)

	var numflees := GetObjProperty(me,"#flees");

	if (numflees)
		if ( numflees > 5 )
			EraseObjProperty(me,"#flees");
			var totem := CInt(GetObjProperty( me, "totem" ));
			if (totem)
				sleep(30);
				return;
			else
				MoveObjectToLocation(me,5288,1176,0,_DEFAULT_REALM, 4);
				RevokePrivilege( me, "invul" );
				SetObjProperty(me,"guardkill",1);
				ApplyRawDamage( me, GetMaxHp(me) + 3 );
			endif
		else
			numflees := numflees + 1;
		endif
	else
		numflees := 1;
	endif

	SetObjProperty(me,"#flees",numflees);

	var runs := 0, sleepdelay;
	while ( (Distance(me, opponent) < 20) && (runs < 50) )
	  sleepdelay := 300 - (CInt(GetDexterity(me)) * 1.5);
	  if (sleepdelay < 0) sleepdelay := 0; endif

		RunAwayFrom(opponent);
		sleepms(sleepdelay);
		runs := runs +1;
		wait_for_event(0);
	endwhile

	RestartScript( me );

endfunction

function GoWild()

	me.script := npccfgfile[me.npctemplate].script;
	me.name := npccfgfile[me.npctemplate].name;
	if( me.name["<random>"] )
		SetName( me, RandomName( me ) );
	endif
	me.setmaster( 0 );
	EraseObjProperty(me, "master");
	SetAnchor(me.x,me.y,10,1 );
	RestartScript(me);

endfunction

function GoToSleep()

	foreach critter in ListMobilesNearLocation( me.x,me.y,me.z, 10 );
		if (!critter.isA(POLCLASS_NPC))
			return 0;
		endif
	endforeach

	return 1;

endfunction
