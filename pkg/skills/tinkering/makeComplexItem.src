use basic;
use os;
use attributes;
use math;
use uo;
use cfgfile;

include "include/attributes";
include "include/canAccess";

var tinker_cfg:= ReadConfigFile("tinker");
var itemcfg := ReadConfigFile("itemdesc");

program make_complex_items(who, item)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(!can_access(who, item))
    return;
  endif
  if(item.movable == 0)
    SendSysMessage(who, "Nie mozesz tego uzyc.", FONT_NORMAL, 40);
    return;
  endif
  if (!ReserveItem(item))
    SendSysMessage(who, "Ktos inny tego uzywa!", FONT_NORMAL, 40);
    return;
  endif
  case(item.objtype)
    0x105b:  make_complex_thing(who, 0x1051, item, 0x1053);     // axle + gear == axle&gear
    0x1053:  make_complex_thing(who, 0x1051, item, 0x105b);     // gear + axle == axle&gear
    0x104d:  make_complex_thing(who, 0x104c, item, 0x104f);     // clock frame + parts == clock
    0x104f:  make_complex_thing(who, 0x104c, item, 0x104d);     // clock parts + frame == clock
    0x1051:  make_complex_thing(who, 0x104f, item, "special");  // axle&gears + springs == clock parts, axle&gears+hinges=sextant parts
    0x105d:  make_complex_thing(who, 0x104f, item, 0x1051);     // springs + axle&gears == clock parts
    0x1059:  make_sextant(who, item);                           // sextent parts == sextant
    0x1055:  make_complex_thing(who, 0x1059, item, 0x1051);     // hinge + axle&gears == sextant parts
    default:
  endcase
endprogram
  
  
function make_complex_thing(who, desired_item, clicked_part, required_part)
    var skill     := tinker_cfg[desired_item].skill;
    var itemname  := tinker_cfg[desired_item].Name;
    var thepoints := skill*2;
    var canexceptional := tinker_cfg[desired_item].exch;
    SendSysMessage(who, "Na czym chcesz tego uzyc?", FONT_NORMAL, COLOR_GREEN);
    var use_on := Target(who, "");
    if (!use_on)
      SendSysMessage(who, "Anulowano.", FONT_NORMAL, 40);
      return;
    endif
    if(!can_access(who, use_on))
      return;
    endif
    if (!Accessible(who, use_on))
      SendSysMessage(who, "Nie mozesz tego dosiegnac!", FONT_NORMAL, 40);
      return;
    endif
    if (!ReserveItem(use_on))
      SendSysMessage(who, "Ktos juz tego uzywa!", FONT_NORMAL, 40);
      return;
    endif
    if(use_on.movable == 0)
      SendSysMessage(who, "Nie mozesz tego uzyc.", FONT_NORMAL, 40);
      return;
    endif
    if (required_part != "special")
      if (use_on.objtype != required_part)
        SendSysMessage(who, "Nie masz pojecia jak polaczyc te przedmioty ze soba.", FONT_NORMAL, 40);
        return;
      endif
    else
      if ((use_on.objtype != 0x105d) and (use_on.objtype != 0x1055))
        SendSysMessage(who, "Nie masz pojecia jak polaczyc te przedmioty ze soba.", FONT_NORMAL, 40);
        return;
      elseif (use_on.objtype == 0x105d)
        desired_item := 0x104f;
      else
        desired_item := 0x1059;
      endif
    endif
    if(use_on.amount  >= 1)
      if(CheckSkill(who, SKILLID_TINKERING, skill,  thepoints))
        set_critical(1);
        SubtractAmount(use_on, 1 );
        SubtractAmount(clicked_part, 1 );
        set_critical(0);
        var theitem := CreateItemInBackpack( who, desired_item, 1 );
        if (theitem)
          if (canexceptional and (CheckSkill(who, SKILLID_TINKERING, 90, thepoints)))
            theitem.quality := EXP_QUALITY;
            theitem.hp := theitem.maxhp;
            SendSysMessage(who, "Zrobiles przedmiot wyjatowej jakosci.", FONT_NORMAL, 90);
          else
            SetName(theitem, itemname );
            SendSysMessage( who, "Zrobiles przedmiot i wlozyles go do plecaka.", FONT_NORMAL, COLOR_GREEN );
          endif
        endif
      else
        SubtractAmount(use_on, 1 );
        SubtractAmount(clicked_part, 1 );
        SendSysMessage(who, "Zniszczyles czesc materialu.", FONT_NORMAL, 40 );
      endif
    else
      SendSysMessage(who, "Masz za malo materialu!", FONT_NORMAL, 40);
    endif
endfunction

function make_sextant(who, sextant_parts)
    var skill     := tinker_cfg[0x1057].skill;
    var itemname  := tinker_cfg[0x1057].Name;
    var thepoints := skill*2;
    if(CheckSkill(who, SKILLID_TINKERING, skill,  thepoints))
      SubtractAmount(sextant_parts, 1 );
      var theitem := CreateItemInBackpack( who, 0x1057, 1 );
      if (theitem)
        if (CheckSkill(who, SKILLID_TINKERING, 90, thepoints))
          theitem.quality := EXP_QUALITY;
          theitem.hp := theitem.maxhp;
          SendSysMessage(who, "Zrobiles przedmiot wyjatkowej jakosci.", FONT_NORMAL, 90);
        else
          SetName(theitem, itemname );
          SendSysMessage( who, "Zrobiles przedmiot i wlozyles go do plecaka.", FONT_NORMAL, 40 );
        endif
      endif
    else
      SubtractAmount(sextant_parts, 1 );
      SendSysMessage(who, "Zniszczyles czesc materialu.", FONT_NORMAL, 40 );
    endif
endfunction
