use os;
use basic;
use uo;

include "include/canAccess";
include "include/attributes";

const LOGOUT_WAIT := 30;
const UOBJECT_CAMPFIRE := 0xde3;

program use_bedroll( who, bedroll )
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(!can_access(who, bedroll))
    return;
  endif
  if(bedroll.movable == 0)
    SendSysMessage(who, "Nie mozesz tego uzyc.", FONT_NORMAL, 40);
    return;
  endif
   var items := ListItemsNearLocationOfType( who.x,who.y,who.z, 2, UOBJECT_CAMPFIRE );
   if( len(items) == 0 )
      SendSysMessage( who, "Mozesz rozbijac oboz w tylko w poblizu ogniska.", FONT_NORMAL, 40);
      return;
   else
      SendSysMessage( who, "Musisz stac spokojnie podczas rozbijania obozu.", FONT_NORMAL, 40);
   endif

   var x := who.x;
   var y := who.y;
   var z := who.z;

   var ttl := LOGOUT_WAIT;

   while( ttl > 0 )
      if( (who.x != x) || (who.y != y) || (who.z != z) )
         SendSysMessage( who, "Przerwales przygotowywanie obozu.", FONT_NORMAL, 40);
         return;
      endif
      SendSysMessage( who, "Musisz poczekac " + ttl + " sekund by zasnac.", FONT_NORMAL, COLOR_GREEN);
      ttl := ttl - 5;
      sleep(5);
   endwhile
   SetObjProperty(who,"camped",1);
   DisconnectClient( who );
endprogram
