use math;
use os;
use util;
use attributes;
use basic;
use cfgfile;
use uo;
use polsys;

include "include/client";
include "include/attributes";
include "include/canAccess";
include "include/others";
include "../../foundations/gumps/gumps";

program do_poisoning(who)

	if(IsBusy(who, 0, 0))
		return;
	endif

	SetBusyTime(who, GetSkillDelay(SKILLID_POISONING));
	
	SendSysMessage(who, "Wskaz trucizne.", FONT_NORMAL, COLOR_GREEN);
	var the_poison := Target(who);
	if(!the_poison)
		SendSysMessage(who, "Anulowano.", FONT_NORMAL, 40);
		return 0;
	endif
	if((!can_access(who, the_poison)) || (!Accessible(who, the_poison)))
		SendSysMessage(who, "Nie mozesz tego dosiegnac!", FONT_NORMAL, 40);
		return 0;
	endif
	if(the_poison.movable == 0)
		SendSysMessage(who, "Nie mozesz uzyc tej mikstury.", FONT_NORMAL, 40);
		return 0;
	endif
	if( !CheckIfPoisonPotion(the_poison) )
		SendSysMessage(who, "Musisz wskazac trujaca miksture!", FONT_NORMAL, 40);
		return 0;
	endif
	if(!ReserveItem(the_poison))
		return 0;
	endif
	SendSysMessage(who, "Wskaz przedmiot, ktory chcesz zatruc.", FONT_NORMAL, COLOR_GREEN);
	var the_item := Target(who, TGTOPT_CHECK_LOS);
	if(!the_item)
		SendSysMessage(who, "Anulowano.", FONT_NORMAL, 40);
		return 0;
	endif
	if((!can_access(who, the_item)) || (!Accessible(who, the_item)))
		SendSysMessage(who, "Nie mozesz tego dosiegnac!", FONT_NORMAL, 40);
		return 0;
	endif
	var acfgfile := ReadConfigFile(":*:itemdesc");
	var theitem := acfgfile[the_item.objtype];
	if(theitem)
		if((theitem.Attribute == "Swords") || (theitem.Attribute == "Fencing") || (theitem.Attribute == "Archery"))
			var stren := 1;
			var skill := 10;
			var point := 50;
			case(the_poison.objtype)
				0xdc04: stren := 1;   skill := 10;   point := 50;
				0xdc05: stren := 2;   skill := 30;   point := 80;
				0xdc06: stren := 3;   skill := 55;   point := 110;
				0xdc17: stren := 4;   skill := 88;   point := 160;
			endcase
			if(SubtractAmount(the_poison, 1))
				CreateItemInBackpack(who, 0xf0e, 1);
				if(CheckSkill(who, SKILLID_POISONING, skill, point))
					PlaySoundEffect(who, 0x248);
					SetObjProperty(the_item, "poison_level", stren);
					SetObjProperty(the_item, "d", (155 + (stren * 5)));
					var hitscripts := GetObjProperty(the_item, "OnHit");
					if(!hitscripts)
						hitscripts := {};
					endif
					var check := 0;
					foreach thing in hitscripts
						if(thing == ":combat:poisonHit")
							check := 1;
						endif
					endforeach
					if(check == 0)
						hitscripts.append(":combat:poisonHit");
						SetObjProperty(the_item, "OnHit", hitscripts);
					endif
					SendSysMessage(who, "Zatrules przedmiot.", FONT_NORMAL, 63);
					var myskill := CInt(GetEffectiveSkill(who, SKILLID_POISONING));
					var modpts := 0;
					if(myskill >=  skill)
						modpts := (myskill - skill);
					endif
				else
					if(RandomInt(100) > 90)
						SendSysMessage(who, "Nie udalo Ci sie i zatrules sam siebie!", FONT_NORMAL, COLOR_GREEN);
						SetObjProperty(who, "poison_level", stren);
						start_script(":spells:poisonDamage", who);
					else
						SendSysMessage(who, "Nie udalo Ci sie zatruc przedmiotu.", FONT_NORMAL, 40);
					endif
				endif
			else
				SendSysMessage(who, "Cos nie tak z ta trucizna.", FONT_NORMAL, COLOR_GREEN);
				return 0;
			endif
		endif
	else
		SendSysMessage(who, "Nie mozesz tego zatruc.", FONT_NORMAL, 40);
	endif
endprogram

function CheckIfPoisonPotion(potion)
	case(potion.objtype)
		0xdc04:
		0xdc05:
		0xdc06:
		0xdc17:
		return 1;
	endcase
	return 0;
endfunction
