// crash
use vitals;
use basicio;
use cfgfile;
use math;
use os;
use basic;
use uo;


include "include/spellRestrictions";
include "include/attributes";


program cast_explosion( parms )
	var circle := 6;
	var delivery := "direct";
	var noto := "harmful";
	var result := maincast(parms, delivery, circle, noto, 43);
	if(result == 0)
		return 0;
	endif
	var caster := result[1];
	var cast_on := result[2];
	if(Reflected(cast_on))
		PlayMovingEffect( cast_on, caster, FX_ENERGY_BOLT, 0xa, 0x1e );
		PlaySoundEffect( cast_on, 776);
		cast_on := caster;
		if (GetObjProperty( caster, "mr") == "1")
			EraseObjProperty(caster, "mr");
			SendSysMessage(caster, "Magiczne lustro ocalilo Cie!");
			return 0;
		endif
	endif
	if(GetObjProperty(cast_on, "LastHit")[2] != caster.serial)
		SetObjProperty(cast_on, "LastHit", ( {caster.name, caster.serial, "explosion" } ) );
	endif
	var magery := Cint(GetEffectiveSkill(caster, SKILLID_MAGERY)/4);
	var eval := Cint(GetEffectiveSkill(caster, SKILLID_EVALINT)/3);
	var wodna := Cint(GetObjProperty(cast_on, "#Wodna"));
	var wodnaqual := wodna * 0.01;
	var int := GetIntelligence(caster);
	var bonus := 0;
	if(caster.ip)
		bonus := int/5;
	endif
	var dmg := magery + eval + bonus;
	if(wodna == 0)
		EraseObjProperty(cast_on, "#Wodna");
	endif
	if(wodna)
		dmg := dmg - (wodnaqual*100);
	endif

	Detach();
	PlaySoundEffect( cast_on, 776 );
	PlayStationaryEffect( cast_on.x,cast_on.y,cast_on.z, FX_EXPLODE_3, 7, 0x10 );
	var cfg := ReadConfigFile("::npcdesc");
	var odbij := cfg[cast_on.npctemplate].Resuje;
	var miekki := cfg[cast_on.npctemplate].Leszcz;
	if(odbij == "ogien")
		dmg := dmg / 2;
		PrintTextAbove(cast_on, "*absorbuje energie*");
	elseif(miekki == "ogien")
		dmg := dmg * 2;
		PrintTextAbove(cast_on, "*dostaje podwojny cios energii*");
	endif
	ApplyRawDamage(cast_on, Resisted(circle,caster,cast_on,dmg));
endprogram
