use uo;
use vitals;

include "include/spellRestrictions";

var npcfg := ReadConfigFile("::npcdesc");

program chrcast_strangle( parms )
	var circle := 6;
	var delivery := "direct";
	var noto := "harmful";
	var result := maincast(parms, delivery, circle, noto, 111);
	if(result == 0)
		return 0;
	endif
	var caster := result[1];
	var cast_on := result[2];
	var sspeak := Cint(GetEffectiveSkill(caster, SKILLID_SPIRITSPEAK));
	var int := GetIntelligence(caster);
	var daemon := Cint(GetObjProperty(cast_on, "#Daemon"));
	var daemonqual := daemon * 0.01;

	var bonus := 0;
	if(caster.ip)
		bonus := int/5;
	endif
	if(Reflected(cast_on))
		cast_on := caster;
	endif
	if(Cint(GetObjProperty(cast_on, "strangle")))
		SendSysMessage(caster, "Cel jest juz pod wplywem czaru.");
		EraseObjProperty (caster, "casting");
		return 0;
	endif
	var dmg := cint (sspeak + bonus )/ 20;

	if(daemon == 0)
		EraseObjProperty(cast_on, "#Daemon");
	endif
	if(daemon)
		dmg := dmg - (daemonqual*100);
	endif

	var waittime := 5;
	var rounds := cint(GetEffectiveSkill(caster, SKILLID_SPIRITSPEAK) / 30);
	var x := 1, dmg2;

	Detach();
	SetObjProperty(cast_on, "strangle", 1);
	var damaged_sound;
	for( x := 1; x <= rounds; x := x + 1)
		if ( cast_on.npctemplate )
			damaged_sound := npcfg[cast_on.npctemplate].damagedsound;		
		else
			damaged_sound := 0x421;
		endif
		PlaySoundEffect(cast_on, damaged_sound);
		Printtextabove(cast_on, "*dusi sie*");
		dmg2 := cint (dmg * (5 - (Getvital(cast_on, "Stamina") / Getvitalmaximumvalue(cast_on, "Stamina")) * 2));
		if(GetObjProperty(cast_on, "LastHit")[2] != caster.serial)
			SetObjProperty(cast_on, "LastHit", {caster.name, caster.serial, "strangle" });
		endif
		ApplyRawDamage(cast_on, Resisted(circle, caster, cast_on, dmg2));
		sleep(waittime);
		if(cast_on.dead)
			break;
		endif
		if(waittime > 1)
			waittime := waittime - 1;
		endif
	endfor
	EraseObjProperty(cast_on, "strangle");
endprogram
