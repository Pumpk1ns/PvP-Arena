use uo;
use os;


include "include/attributes";
include "include/tempMod";

CONST WARNING_DELAY := 30; // in seconds

program processtempmods( parms )

	set_script_option( SCRIPTOPT_CAN_ACCESS_OFFLINE_MOBILES , 1 );

	var who	:= parms[1];
	var stat	:= parms[2];
	var amt	:= Cint(parms[3]);
	var duration	:= Cint(parms[4]);

	detach();

	if( (!who.poisoned) )
		// this person should not be poisoned
		if ( GetHpRegenRate(who) != BaseRegenRate(who) )
			SetHpRegenRate(who , ( BaseRegenRate(who)) * 12);
			who.setPoisoned(0);
		endif
	endif

	Set_Critical(1);
	var allmods	:= GetObjProperty( who , "#mods" );
	if (!allmods)
		allmods := {};
	endif

	var mmod := {};
	mmod[1] := stat;
	mmod[2] := amt;
	mmod[3] := ReadGameClock() + duration;

	var pos := AddToStatMods( allmods , mmod );
	if( !pos )
		return;
	endif

	case( mmod[1] )
		"cstr":
        	"str" : SetStrengthMod( who, GetStrengthMod( who ) + mmod[2] );

		"cdex":
        	"dex" : SetDexterityMod( who, GetDexterityMod( who ) + mmod[2] );

		"cint":
        	"int" : SetIntelligenceMod( who, GetIntelligenceMod( who ) + mmod[2] );

		"car":
        "ar"  : who.ar_mod := who.ar_mod + mmod[2];

		"p"   :	who.SetParalyzed();

		"call":
        	"all" : SetStrengthMod( who, GetStrengthMod( who ) + mmod[2] );
			SetDexterityMod( who, GetDexterityMod( who ) + mmod[2] );
			SetIntelligenceMod( who, GetIntelligenceMod( who ) + mmod[2] );

		"cpoly":
		"poly":
                        SetStrengthMod(who, GetStrengthMod( who ) + mmod[2] );
			SetDexterityMod(who, GetDexterityMod( who ) + mmod[2] );
			SetIntelligenceMod(who, GetIntelligenceMod( who ) + mmod[2] );
			who.ar_mod := who.ar_mod + cint(mmod[2]);
	endcase

	SetObjProperty( who , "#mods" , allmods );
	Set_Critical(0);
	var modif_duration := duration - WARNING_DELAY;
	if(mmod[1] != "p")
		Sleep(modif_duration);
	else
		sleep(duration);
	endif
	var mod_name := "";
	if(mmod[1] == "str")
		mod_name := "sila";
	elseif(mmod[1] == "int")
		mod_name := "inteligencja";
	elseif(mmod[1] == "dex")
		mod_name := "zrecznosc";
	elseif(mmod[1] == "ar")
		mod_name := "protekcja";
	elseif(mmod[1] == "all")
		mod_name := "sila, zrecznosc, inteligencja";
	else
		mod_name := "niezidentyfikowany";
	endif	
	if(mmod[1] != "p")
		SendSysMessage(who, "Za " + WARNING_DELAY + " sekund zakonczy sie dzialanie mikstury/czaru: " + mod_name, 3, 40);
		sleep(WARNING_DELAY);
	endif
	if( who )
		Set_Critical(1);
		allmods := GetObjProperty( who , "#mods" );

		if( !allmods )
			Set_Critical( 0 );
			return;
		endif

		var newmods := {};
		var i;
		for( i := 1 ; i <= len(allmods) ; i := i + 1 )
			if( allmods[i][1] == mmod[1] and ReadGameClock() >= allmods[i][3] )
				UndoTheMod( who , allmods[i][1] , allmods[i][2] );
			else
				newmods.append( allmods[i] );
			endif
		endfor

		if( len(newmods) )
			SetObjProperty( who , "#mods" , newmods );
		else
			EraseObjProperty( who , "#mods" );
		endif

		Set_Critical( 0 );
	endif

endprogram
