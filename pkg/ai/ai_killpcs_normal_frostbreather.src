use os;
use uo;
use npc;
use util;
use cfgfile;

include "include/attributes";
include "include/npc_util";
include "include/npcinfo";
include "include/eventID";
include "setup/setup";
include "include/events";
include "main/mainloop_killpcs_normal";
include "main/sleepmode_normal";
include "combat/fightmode_normal";
include "combat/changeopponent_default";
include "combat/closedistance_default";
include "combat/gethelp_default";
include "combat/pathfinding";
include "include/dragons/dragons";

const HALT_THRESHOLD := 14; // how close before he attacks?
drop_anchor();

program KillPlayers ()
	start_script("::NPCKeeper", me);

	if (GetObjProperty (me, "frozen"))
		me.frozen := 1;
	endif

	post_combat ();
	main_AI_loop ();
endprogram


function process_combat_event (byref ev)
	BreathFire (ev.source);
	if (ev.type == EVID_DAMAGED)
		if (GetVital (me, "Life") < CINT (GetVitalMaximumValue (me, "Life")/10))
			EraseObjProperty (me, "#flees");
			Flee (ev.source);
		endif
	endif
endfunction


function in_combat_event_loop (byref opponent)
	BreathFire (opponent);
endfunction

function post_combat()
	ClearEventQueue ();
	DisableCombatEvents();
	EnableMainEvents();

	SetWarMode ( 0 );
	sleep(1);
	look_around();
endfunction

function prepare_for_fight (byref opponent)
	ClearEventQueue ();
	DisableMainEvents();
	EnableCombatEvents();
endfunction

function BreathFire (byref opponent)
	if (Distance (me, opponent) > 3)
		return;
	endif

	var nextbreath := GetObjProperty (me, "#lastbreath");
	if (!nextbreath)
		SetObjProperty (me, "#lastbreath", ReadGameClock() + 2 + RandomInt (5));
		return;
	elseif (nextbreath > ReadGameClock())
		return;
	endif

	SetObjProperty (me, "#lastbreath", ReadGameClock() + CINT (RandomInt(10)+10) );
	var torch := CreateItemInContainer (me.backpack, 0x0a12, 1);
	EquipItem (me, torch);

	//Different NPC graphics require different animations
	case (me.graphic)
		CID_DRAGON_GREY:
		CID_DRAGON_RED:
		CID_DRAKE_GREY:
		CID_DRAKE_RED:
			PerformAction (me, 0x0c);
		default:
			performaction (me, 0x5);
	endcase
	PrintTextAbove (me, "*zionie mrozem*", 3, 40);

	var MyTemplateElem := GetNpcTemplateElem (me.npctemplate);
	var firedamage := GetConfigString (MyTemplateElem, "flamestrength");
	if (!firedamage)
		firedamage := "10";
	endif


	foreach mobile in ListMobilesNearLocationEx (me.x, me.y, me.z, 5, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN)
		if (!mobile.hidden or !GetObjProperty (mobile, "#specialhidden_nodamage"))
			if (mobile != me and !mobile.npctemplate["humuc"])
				var dmg := CINT (firedamage);
				if (!dmg or dmg < 5)
					dmg := 5;
				endif
				PlayMovingEffect (me, mobile, MOVING_EFFECT_FROST, 10, 10, 0);
				ApplyRawDamage(mobile, dmg);
			endif
		endif
	endforeach

	sleep (1);
	DestroyItem (torch);
endfunction

