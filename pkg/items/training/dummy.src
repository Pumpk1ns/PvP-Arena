// crash

use attributes;
use os;
use cfgfile;
use math;
use uo;

include "include/client";
include "include/attributes";
include "include/objtype";
include "include/dist";

program training_dummy(who, dummy)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(Distance(who, dummy) > 1)
    SendSysMessage(who, "Musisz stac obok kukly by ternowac.");
    return;
  endif
  var cfg := ReadConfigFile(":combat:itemdesc");
  var attribid := cfg[who.weapon.objtype]."Attribute";
  if(GetObjProperty(dummy, "#swinging"))
    SendSysMessage(who, "Ta kukla jest juz uzywana przez kogos innego.");
    return;
  endif
  var chk := 0;
  case(attribid)
    "Wrestling": CheckSwing(who, SKILLID_WRESTLING, 0x9, dummy);
    "Swords":    CheckSwing(who, SKILLID_SWORDSMANSHIP, ANIM_ATTACK_1HAND_WIDE, dummy);
    "Mace":      CheckSwing(who, SKILLID_MACEFIGHTING, ANIM_ATTACK_1HAND_DOWN, dummy);
    "Fencing":   CheckSwing(who, SKILLID_FENCING, ANIM_ATTACK_1HAND_JAB, dummy);
    "Archery":   SendSysMessage(who, "Aby trenowac lucznictwo musisz uzywac tarczy strzeleckiej.");
    default:     chk := 1;
  endcase
  if(chk == 1)
    SendSysMessage(who, "Nie nauczysz sie wladania ta bronia na kukle.");
  endif
endprogram

function CheckSwing(who, skillid, action, dummy)
  PerformAction(who, action);
  sleepms(500);
  Detach();
  if(GetEffectiveSkill(who, skillid) >= 25.0)
    SendSysMessage(who, "Nie nauczysz sie juz niczego nowego podczas treningu z kukla.");
  else
    CheckSkill(who, skillid, 10, 40);
    if(GetEffectiveSkill(who, SKILLID_TACTICS) < 25)
      CheckSkill(who, SKILLID_TACTICS, 10, 20);
    endif
  endif
  PlaySoundEffect(who, SFX_13C);
  SetObjProperty(dummy, "#swinging", 1);
  if(dummy.objtype == UOBJ_DUMMYFREEZE_NS)
    dummy.graphic := UOBJ_DUMMYSWING_NS;
    sleepms(3600);
    dummy.graphic := UOBJ_DUMMYFREEZE_NS;
  elseif(dummy.objtype == UOBJ_DUMMYFREEZE_EW)
    dummy.graphic := UOBJ_DUMMYSWING_EW;
    sleepms(3600);
    dummy.graphic := UOBJ_DUMMYFREEZE_EW;
  endif
  EraseObjProperty(dummy, "#swinging");
endfunction
